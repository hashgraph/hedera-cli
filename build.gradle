plugins {
    id "org.springframework.boot" version "2.2.0.RELEASE"
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'java'
    // Apply the application plugin to add support for building a CLI application
    id 'application'
    id 'org.jetbrains.dokka' version '0.9.18'
    id 'net.researchgate.release' version '2.6.0'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.4'
}

release {
    failOnPublishNeeded = false
    failOnUpdateNeeded = false
    tagTemplate = 'v$version'
}

group = 'hedera-cli'
// version is specified in gradle.properties

repositories {
    // Use jcenter for resolving dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
    mavenCentral()
    maven {
        url 'https://repo.spring.io/milestone'
    }
}

dependencies {
    // This dependency is used by the application.
    implementation 'com.google.guava:guava:27.1-jre'

    implementation "org.springframework.boot:spring-boot-starter-web"

    // hedera sdk
    implementation 'com.hedera.hashgraph:sdk:1.1.1'
    // netty transport (for high throughput applications)
    implementation 'io.grpc:grpc-netty-shaded:1.24.0'

    // dependencies for crypto functions
    compile group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.64'
    compile group: 'net.i2p.crypto', name: 'eddsa', version: '0.3.0'

    // dotenv
    compile 'io.github.cdimascio:java-dotenv:5.1.0'
    compile "joda-time:joda-time:2.2"

    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'

    // picocli
    compile 'info.picocli:picocli:4.0.4'

    // hjson
    implementation 'org.hjson:hjson:3.0.0'

    // json
    implementation 'com.fasterxml.jackson.core:jackson-core:2.10.1'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.10.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.10.1'

    // kotlin reflection: provided to disable log warning
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.3.41"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin"

    // lombok
    implementation "org.projectlombok:lombok:1.18.10"
    annotationProcessor "org.projectlombok:lombok:1.18.10"

    // springboot implementation
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.shell:spring-shell-starter:2.0.0.RELEASE'

    // spring shell test support
    testImplementation 'org.springframework.shell:spring-shell-core-test-support:2.0.0.RELEASE'

    // Mockito and JUnit 5
    testImplementation('org.junit.jupiter:junit-jupiter-api:5.5.2')
    testCompile('org.junit.jupiter:junit-jupiter-params:5.5.2')
    testRuntime('org.junit.jupiter:junit-jupiter-engine:5.5.2')
    testCompile "org.mockito:mockito-inline:3.2.4"
    // testImplementation 'org.mockito:mockito-inline:2.13.0'
    testCompile('org.mockito:mockito-junit-jupiter:2.18.3')
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
	    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}

    // capture log during tests for assertions
    testImplementation("io.github.netmikey.logunit:logunit-core:1.1.0")
    // log4j2 only
    testRuntimeOnly("io.github.netmikey.logunit:logunit-jul:1.1.0")
}

application {
    // Define the main class for the application
    mainClassName = 'com.hedera.cli.Application'
}

processResources {
    filesMatching('application.properties') {
        expand(project.properties)
    }
}

dokka {
    outputFormat = 'markdown' 
    outputDirectory = "$buildDir/javadoc"
}

test {
    useJUnitPlatform {
        excludeTags 'integration'
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(
                dir: it,
                exclude: [
                    'com/hedera/cli/Application.class'
                ]
            )
        }))
    }
}

coveralls {
    jacocoReportPath 'build/reports/jacoco/test/jacocoTestReport.xml'
    saveAsFile = true
    sendToCoveralls = false
}

bootJar {
	launchScript()
}

// codacy
configurations { codacy }

repositories {
    maven { url "https://jitpack.io" }
    maven { url "http://dl.bintray.com/typesafe/maven-releases" }
}

dependencies {
    codacy 'com.github.codacy:codacy-coverage-reporter:-SNAPSHOT'
}

task sendCoverageToCodacy(type: JavaExec, dependsOn: jacocoTestReport) {
    main = "com.codacy.CodacyCoverageReporter"
    classpath = configurations.codacy
    args = [
            "report",
            "-l",
            "Java",
            "-r",
            "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
    ]
}

apply plugin: 'io.spring.dependency-management'
apply plugin: 'java'
apply from: 'integration-test.gradle'